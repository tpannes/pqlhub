/* PQL script "ExportIcons" created 2012 by TimoP
 * Version 1.0
 * 
 * Description:  
 *   The script exports icons from IconCollection to the filesystem.
 * 
 * 
 * Return:
 *   Number of exported icons 
 *  
 * 
 */


//@MSG_ERROR   = 'PFGM0001E';
//@MSG_WARNING = 'PFGM0002W';
@MSG_INFO    = 'PFGM0003I';


@self   = $self.$id,$node,*;
@class  = @self\$ClassGUID.$InternalName;
@node   = @self.$node;
@method = 'ExportIcons';
@FILE_EXTENSION = 'ico';
@PLATFORM_ID_WINDOWS = 'w3';
@DIR_SEPARATOR_WINDOWS = '\';
@DIR_SEPARATOR_UNIX    = '\';

// 1. get input parameters
@exportSelection = @self.IconExportFilter;
@exportFolder    = @self.IconExportPath;
@verbodeLevel    = @self.VerboseLevel;

if !(right(@exportFolder,1) like array(@DIR_SEPARATOR_WINDOWS,@DIR_SEPARATOR_UNIX)) then
  if (@node.Platform==@PLATFORM_ID_WINDOWS) then
    @dirSeparator = @DIR_SEPARATOR_WINDOWS;
  else
    @dirSeparator = @DIR_SEPARATOR_UNIX;
  endif
  @exportFolder += @dirSeparator;
endif


// 2. get icons
@iconCollection = {b8e28152-6d6d-11d6-81d6-f4f368e29511};
@iconSet = @iconCollection\(ICON:.$name like @exportSelection).$id,$name,*;
@iconCount = count(@iconSet);

// 3. export icons
@idx = 1;
if (@verbodeLevel>0) then
  message(@MSG_INFO, '['+@class+'::'+@method+'] '+@iconCount+' icons will be exported to folder '+@exportFolder); 
endif
forEach @icon in (@iconSet)
  @iconName = @icon.$name;
  @iconName = fromMem(@self.GetFileNameFromStringGeneric(@iconName).Return1);
  @exportPath = @exportFolder + @iconName + '.' + @FILE_EXTENSION;
  if (@verbodeLevel>2) then
    message(@MSG_INFO, '['+@class+'::'+@method+'] -> processing icon "'+@iconName+'" ('+@idx+' of '+@iconCount+')'); 
  endif
  @self.IconDataMem = @icon.Data;
  @self.IconExportFilename = @exportPath;
  @self.ExportIconIntern();
  @idx++;
Next
@self.IconDataMem = $Invalid;
@self.IconExportFilename = "";


return @iconCount;